group = "com.complexible.stardog.examples.api"

dependencies {
	// server core modules
	compile "com.complexible.stardog:server:${stardogVersion}"

	// client modules
	compile "com.complexible.stardog:client-embedded:${stardogVersion}"
	compile "com.complexible.stardog:client-http:${stardogVersion}"

	// external apis
	compile "com.complexible.stardog.sesame:stardog-sesame-core:${stardogVersion}"
	compile "com.complexible.stardog.jena:stardog-jena:${stardogVersion}"
	compile ("com.complexible.stardog.gremlin:stardog-gremlin:${stardogVersion}") {
		exclude group: "com.carrotsearch"
	}
}

task execute(type:JavaExec, dependsOn: compileJava) {

	if (project.hasProperty("mainClass")) {
		main = project.mainClass
	}
	else {
		main = "com.complexible.stardog.examples.api.ConnectionAPIExample"
	}

	classpath = sourceSets.main.runtimeClasspath

	if (project.hasProperty("stardogHome")) {
		jvmArgs = [ "-Dstardog.home=${project.stardogHome}" ]
	}
}

task executeAll(dependsOn: compileJava) << {
	sourceSets.main.allJava.each { File aFile ->
		if (aFile.getName().contains("Example")) {
			runExample aFile.getAbsolutePath()
		}
	}
}

task docs << {
	exec {
		commandLine "docco",
					"-o",
					"${distPath}/docs",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionPoolsExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ExplanationExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/WaldoAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/VersioningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ReasoningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ProvSkosExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ICVExample.java",
					"${srcPath}/com/complexible/stardog/examples/sesame/SesameExample.java",
					"${srcPath}/com/complexible/stardog/examples/jena/JenaExample.java",
					"${srcPath}/com/complexible/stardog/examples/gremlin/TinkerPop3Example.java",
					"${srcPath}/com/complexible/stardog/examples/gremlin/StardogGraphComputerExample.java",
					"${srcPath}/com/complexible/stardog/examples/gremlin/Util.java"
	}
}

def runExample(String example) {
	// replace / with .
	String fqcn = example.replaceAll(System.getProperty("file.separator"), ".");
	// strip .java
	fqcn = fqcn.substring(0, fqcn.length() - 5);
	fqcn = fqcn.substring(fqcn.indexOf("com.complexible"));

	javaexec {

		main = fqcn

		classpath = project(':examples:api').sourceSets.main.runtimeClasspath

		if (project.hasProperty("stardogHome")) {
			jvmArgs = [ "-Dstardog.home=${stardogHome}" ]
		}
		// else stardog will attempt to use its own $STARDOG_HOME
	}
}
